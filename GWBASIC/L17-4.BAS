10 REM Racecar--finished version.
100 DEFINT A-Z:SCREEN 0,0:COLOR 7,0,0:WIDTH 40:LOCATE ,,0:CLS:KEY OFF
110 RANDOMIZE(VAL(RIGHT$(TIME$,2)))
120 TRKWIDTH=9:TRKPOS=15
130 NUMLINES=3:TRKLEN=200
140 CARPOS=TRKPOS+TRKWIDTH\2:CARDIR=0
150 FOR I=1 TO 23:LOCATE 24,TRKPOS:                                                 PRINT "]]" SPC(TRKWIDTH) "[[":NEXT I
160 NUMCRASHES=0:LOCATE 25,13:PRINT "Crashes=";:LOCATE 25,23:                        PRINT NUMCRASHES;
170 FOR TRKCNT=1 TO TRKLEN
180   TRKDIR=1-INT(RND(100)*3)
190   FOR TRKSECTION=1 TO NUMLINES
200     TRKPOS=TRKPOS+TRKDIR:IF (TRKPOS<2) OR (TRKPOS>27) THEN                          TRKPOS=TRKPOS-2*TRKDIR:TRKDIR=-TRKDIR
210     LOCATE 24,TRKPOS:PRINT "]]" SPC(TRKWIDTH)"[["
220     K$=INKEY$:IF K$="z" THEN CARDIR=-1 ELSE IF K$="x" THEN                          CARDIR=0 ELSE IF K$="c" THEN CARDIR=1
230     CARPOS=CARPOS+CARDIR:IF SCREEN(10,CARPOS)=32 GOTO 280
240       IF SCREEN(10,CARPOS)=91 THEN CARPOS=CARPOS-2
250       IF SCREEN(10,CARPOS)=93 THEN CARPOS=CARPOS+2
260       BEEP:BEEP:BEEP
270       NUMCRASHES=NUMCRASHES+1
280     LOCATE 10,CARPOS:PRINT "^";
290     LOCATE 25,23:PRINT NUMCRASHES;
300   NEXT TRKSECTION
310 NEXT TRKCNT
320 FOR J=1 TO 1000:NEXT J
330 IF INKEY$<>"" THEN 320
340 LOCATE 24,7:PRINT "PRESS ANY KEY TO CONTINUE";
350 A$=INKEY$:IF A$="" THEN 350 ELSE CLS
360 END
