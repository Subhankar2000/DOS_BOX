PPROP "X "BURY "TRUE
PPROP "PRINC "BURY "TRUE
PPROP ".SYSTEM "BURY "TRUE
TO WRITEBUFF 
IF EMPTYP :BUFF [PR CHAR 26 STOP]
PR FIRST :BUFF
MAKE "BUFF BF :BUFF
WRITEBUFF
END

TO WRITEOK? 
MAKE "WRITEOK "FALSE
CATCH "ERROR [CLOSE "INVENT.DAT MAKE "WRITEOK "TRUE]
IF NOT :WRITEOK = "TRUE [PR [DISK IS WRITE - PROTECTED] WAIT 50]
CLOSEALL
END

TO WRDATA 
OPEN "INVENT.DAT
SETWRITE "INVENT.DAT
PR :NAME
PR :VALUE
SETWRITE "CON
WRITEOK?
END

TO FINDNAME 
LOCAL "ANS
MAKE "POS READPOS
MAKE "ANS RL
IF :ANS = :NAME [CHANGE STOP]
IF READEOFP [PR "  PR [Can't find the listing] PR "  PR [TYPE 1 TO VIEW, 3 TO EDIT, ESC TO EXIT] CLOSEALL RKEY STOP]
FINDNAME
END

TO CHANGE 
MAKE "BUFF []
LOADBUFF
LOCAL "KEY
MAKE "VAL FIRST :BUFF
MAKE "BUFF BF :BUFF
CT
SETCURSOR [3 3]
PR SE [Item :] :NAME
SETCURSOR [5 3]
PR SE [Stated value : $] :VAL
SETCURSOR [8 2]
PR [Do you want to :]
SETCURSOR [10 2]
PR [1 Change the name of the item]
SETCURSOR [12 2]
PR [2 Change the stated value]
SETCURSOR [14 2]
PR [3 Remove the listing altogether]
SETCURSOR [16 2]
PR [ESC to leave the listing unchanged]
PR " 
MAKE "KEY RC
MODIFY
END

TO HANDLEFILE 
OPEN "INVENT.DAT
SETWRITE "INVENT.DAT
SETWRITEPOS :POS
WRITEBUFF
MAKE "LAST "FALSE
SETWRITE "CON
WRITEOK?
END

TO FIXBUFF 
MAKE "BUFF FPUT :VAL :BUFF
MAKE "BUFF FPUT :NAME :BUFF
END

TO SIZER :LIST
IF EMPTYP :LIST [OP 0]
IF (COUNT :LIST) = 1 [OP COUNT FIRST :LIST]
OP (COUNT FIRST :LIST) + (SIZER BF :LIST) + 1
END

TO MODIFY 
LABEL "NEWNAME
IF :KEY = 1 [TYPE [Enter new name :] MAKE "NAME RL MAKE "SIZE SIZER :NAME (IF :SIZE = 0 [PR [I NEED A NAME...] GO "NEWNAME]) (IF :SIZE > 20 [PR [20 CHARACTERS MAXIMUM] GO "NEWNAME]) FIXBUFF HANDLEFILE STOP]
LABEL "WRONGNUM
IF :KEY = 2 [TYPE [Enter new value : $] MAKE "VAL RW IF NUMBERP :VAL = "FALSE [PR [VALUE NOT NUMERIC] GO "WRONGNUM] [IF OR :VAL > 99999 :VAL < 0 [PR [A VALUE FROM $0 TO $99999, PLEASE] GO "WRONGNUM]] FIXBUFF HANDLEFILE STOP]
IF :KEY = 3 [IF EMPTYP :BUFF [HANDLEFILE STOP] [MAKE "NAME FIRST :BUFF MAKE "BUFF BF :BUFF MAKE "VAL FIRST :BUFF MAKE "BUFF BF :BUFF HANDLEFILE STOP]]
IF :KEY = CHAR 27 [SHOWFILE RKEY STOP]
MAKE "KEY RC
MODIFY
END

TO PRINTLOOP 
SETREAD "INVENT.DAT
IF READEOFP [STOP]
TYPE SE [Item :] RL
MAKE "AMT RW
SETCURSOR SE FIRST CURSOR 28
PR SE [$] FORM :AMT 5 2
MAKE "TOTAL :AMT + :TOTAL
PRINTLOOP
END

TO LOADBUFF 
IF READEOFP [CLOSEALL STOP]
MAKE "BUFF LPUT RL :BUFF
LOADBUFF
END

TO EDDATA 
CT SETTC [0 7]
SETCURSOR [1 5]
REPEAT 18 [TYPE "\ ]
SETCURSOR [2 5]
PR [\ EDITING THE FILE\ ]
SETCURSOR [3 5]
REPEAT 18 [TYPE "\ ]
SETTC [7 0]
SETCURSOR [9 3]
TYPE [Enter the name of the listing]
SETCURSOR [10 3]
TYPE [to be changed :]
MAKE "NAME RL
OPEN "INVENT.DAT
SETREAD "INVENT.DAT
FINDNAME
END

TO SHOWFILE 
CT
SETCURSOR [3 3]
PR [HOUSEHOLD INVENTORY]
PR "  PR " 
OPEN "INVENT.DAT
MAKE "TOTAL 0
PRINTLOOP
PR "  PR " 
CLOSEALL
SETCURSOR SE FIRST CURSOR 18
PR SE [TOTAL: $] FORM :TOTAL 6 2
PR " 
PR [2 TO PRINT, 3 TO EDIT, ESC TO EXIT]
END

TO ADDATA 
CT
SETCURSOR [7 5]
LABEL "ENTERNAME
TYPE [Item name :]
MAKE "NAME RL
MAKE "SIZE SIZER :NAME
IF :SIZE = 0 [PR [I NEED A NAME...] SETCURSOR SE FIRST CURSOR 5 GO "ENTERNAME]
IF :SIZE > 20 [PR [MAXIMUM NAME LENGTH IS 20 CHARACTERS] SETCURSOR SE FIRST CURSOR 5 GO "ENTERNAME]
LABEL "ENTERVAL
SETCURSOR SE FIRST CURSOR 5
TYPE [Value: $]
MAKE "VALUE RW
IF NUMBERP :VALUE = "FALSE [PR [PLEASE ENTER A NUMBER] GO "ENTERVAL]
IF OR :VALUE > 99999 :VALUE < 0 [PR [A VALUE FROM $0 TO $99999, PLEASE] GO "ENTERVAL]
WRDATA
PR "
SETCURSOR SE FIRST CURSOR 5
TYPE [Any more items to add?]
MAKE "ANS RL
IF EMPTYP :ANS = "FALSE [IF FIRST FIRST :ANS = "Y [ADDATA]]
END

TO EDFILE 
CT
LOCAL "ANS
SETCURSOR [5 2]
TYPE [Do you want to make an entry?]
MAKE "ANS RL
IF EMPTYP :ANS = "FALSE [IF FIRST FIRST :ANS = "Y [ADDATA SHOWFILE STOP]]
SETCURSOR SE FIRST CURSOR 2
TYPE [Do you want to change a listing?]
MAKE "ANS RL
IF EMPTYP :ANS = "FALSE [IF FIRST FIRST :ANS = "Y [EDDATA]]
SHOWFILE
END

TO RKEY 
LOCAL "KEY
MAKE "KEY RC
IF :KEY = 1 [SHOWFILE]
IF :KEY = 2 [CATCH "ERROR [DRIBBLE "LPT1 SHOWFILE NODRIBBLE RKEY] PR [CAN'T PRINT WITHOUT A PRINTER]]
IF :KEY = 3 [EDFILE]
IF :KEY = CHAR 27 [DEMO]
RKEY
END

TO START 
CLOSEALL
SETPRECISION 10
CT TS
SETCURSOR [2 8]
PR [HOUSEHOLD INVENTORY FILE]
SETCURSOR [4 3]
PR [This program is a demonstration of]
PR [IBM Logo's file handling capabilities.]
PR [A disk file of household articles is]
PR [created, and may be edited, updated,]
PR [viewed or printed, following a simple]
PR [menu format. The file consists of an]
PR [item name and the value of that item.]
PR [Sample entries are provided.]
PR [IF A WRITE - PROTECT TAB IS ON THE]
PR [DISKETTE, FILE EDITING IS IMPOSSIBLE.]
SETCURSOR [15 10]
PR [Do you want to:]
SETCURSOR [16 12]
PR [1 VIEW THE FILE]
SETCURSOR [17 10]
PR [* 2 PRINT THE FILE]
SETCURSOR [18 12]
PR [3 ADD TO / EDIT THE FILE]
SETCURSOR [20 1]
PR [* parallel printer must be connected.]
SETTC [0 7]
SETCURSOR [22 2]
PR [\ TYPE THE NUMBER OF YOUR CHOICE \ \ \ ]
SETCURSOR [23 2]
PR [\ OR ESC TO RETURN TO THE MAIN MENU.]
SETTC [7 0]
MAKE "WRITEOK "TRUE
RKEY
CLOSEALL
END

MAKE "DUMMY "S
MAKE "AMT 350
MAKE "VALUE 350
MAKE "VAL [350]
MAKE "SIZE 9
MAKE "KEY 1
MAKE "TOTAL 8606.3
MAKE "BUFF []
MAKE "WRITEOK "TRUE
MAKE "POS 115
MAKE "RUN "3
MAKE "NAME [LAWNMOWER]
MAKE "LAST "FALSE
