PPROP "X "BURY "TRUE
PPROP "PRINC "BURY "TRUE
PPROP ".SYSTEM "BURY "TRUE
TO DESCRIP 
CS
TITLE -110 100 [THE TOWERS OF HANOI]
SETCURSOR [5 0]
PR [The puzzle begins with a "tower"]
PR [of discs, arranged in order of size,]
PR [with the largest at the bottom and]
PR [the smallest at the top.]
PR []
PR [The task is to move them, one by one,]
PR [from the Start pole to the Goal pole,]
PR [without placing a larger disc on a]
PR [smaller disc.]
PR " PR "
PR [It's not easy; even the computer]
PR [takes a while to figure it out!]
PR []
PR [Select the number of discs :]
SETCURSOR [21 8]
TYPE [( 14 maximum )]
END

TO MAKENUM 
MAKE "NUM RW
IF EMPTYP :NUM [TYPE [I NEED A NUMBER...] MAKENUM]
IF NUMBERP :NUM = "FALSE [TYPE [A NUMBER, PLEASE...] MAKENUM]
IF NUMBERP [] [MAKENUM]
IF OR :NUM < 1 :NUM > 14 [TYPE [A NUMBER BETWEEN 1 AND 14, PLEASE:] MAKENUM]
END

TO 3STICKS 
SETPC 1
PU
SETY -50
STICK
SETX 80
STICK
SETX -80
STICK
TITLE -95 110 [THE TOWERS OF HANOI]
TITLE -103 -80 [START]
TITLE -23 -80 [SPARE]
TITLE 64 -80 [GOAL]
PU SETPOS [-80 -50]
END

TO DISKS :SMALL :BIG
IF :SMALL > :BIG [SETY -50 STOP]
IF :BIG = :NUM [FD 10] [FD 8]
PD
DISQ :BIG
DISKS :SMALL :BIG - 1
END

TO SEQUENCE :N
OP IF :N = 1 [[1]] [SE SEQUENCE :N - 1 :N]
END

TO HANOI :NUM :START :SPARE :GOAL
IF :NUM = 0 [STOP]
HANOI :NUM - 1 :START :GOAL :SPARE
MOVE.ONE :START :GOAL
HANOI :NUM - 1 :SPARE :START :GOAL
END

TO MOVE.ONE :FROM :TO
MAKE "CO :CO + 1
ELIM :FROM
ADD :TO
END

TO ELIM :STICK
MOVETO :STICK
MAKE "THIS FIRST :DSK
PE
DISQ :THIS
SETY -50
MAKE :STICK BF :DSK
END

TO DISQ :SIZE
SETPC COLOR :SIZE
MAKE "SIZE :SIZE * :FACT
LT 90
FD :SIZE
BK :SIZE * 2
RT 90 FD 1
LT 90
FD :SIZE * 2
RT 90 FD 1
RT 90
FD :SIZE * 2
BK :SIZE
LT 90
IF FIRST PEN = "PENERASE [SETPC 1 PD BK 3 FD 3]
PU
END

TO ADD :STICK
MOVETO :STICK
FD 10
PD
DISQ :THIS
SETY -50
MAKE :STICK SE :THIS :DSK
END

TO STICK 
PD
FD 140
BK 140
BOTTOMSTICK POS
END

TO MOVETO :STICK
PU
SETX (BF :STICK) * 80 - 160
MAKE "DSK THING :STICK
SETY 10 * (COUNT :DSK) - 50
END

TO COLOR :N
OP 1 + ROUND 3 * ((QUOTIENT :N 3) - (INT QUOTIENT :N 3))
END

TO BOTTOMSTICK :POS
RT 90
SETX XCOR - 30
REPEAT 2 [FD 60 RT 90 FD 10 RT 90]
PU
SETPOS SE XCOR + 5 YCOR - 5
PD
FILL
PU
SETPOS :POS
SETH 0
END

TO START 
CT CS
DESCRIP
MAKENUM
IF :NUM > 7 [MAKE "FACT 2] [MAKE "FACT 4]
CS
HT
FULLSCREEN
3STICKS
DISKS 1 :NUM
MAKE "CO 0
MAKE "S2 []
MAKE "S3 []
MAKE "S1 SEQUENCE :NUM
HT
HANOI :NUM "S1 "S2 "S3
CT
SETTEXT 3
PR (SE [IT TOOK] :CO [MOVES FOR] :NUM "DISCS)
WAIT 50
PR [Press any key to play again, or]
PR [Esc to return to the main menu.]
WAITKEY
MAKE "DUMMY RC
IF :DUMMY = CHAR 27 [CS HT CT DEMO]
START
END

MAKE "KEY 1
MAKE "RUN "0
