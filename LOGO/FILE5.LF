PPROP "PRINC "BURY "TRUE
PPROP "X "BURY "TRUE
PPROP ".SYSTEM "BURY "TRUE
TO EXPL 
CT TS
SETCURSOR [3 15]
PR [HANGMAN]
SETCURSOR [6 0]
PR [The object of the Hangman game]
PR [is to guess the letters of a word.]
PR []
PR [The number of letters in the word]
PR [is shown by underlines ( _ _ _ _ ) .]
PR []
PR [Each time you guess the right letter,]
PR [it appears in its place in the word]
PR [on the screen. If you are wrong,]
PR [the computer draws another part of]
PR [the hanging man's body.]
SETCURSOR [20 0]
PR [Press any key to begin, or Esc to]
PR [return to the main menu.]
END

TO HANGMAN 
BEGIN
SETTEXT 5
CS HT HANGER
PLAY
IF EMPTYP :PART [PR SE [YOU LOSE, THE WORD IS] :WORD]
IF NOT MEMBERP "- :NEWWORD [PR :NEWWORD PR (SE [* * * YOU WIN IN] COUNT :GIVEN [TRIES * * *])]
PR [Press any key to continue, Esc to exit]
WAITKEY
MAKE "DUMMY RC
IF :DUMMY = CHAR 27 [CS CT HT DEMO STOP]
HANGMAN
END

TO BEGIN 
CLEARTEXT
MAKE "GIVEN []
MAKE "NEWWORD []
MAKE "WORD WORDS
MAKE "LENGTH COUNT :WORD
MAKE "PART [HEAD NECK RTARM LTARM BODY RTLEG LTLEG]
REPEAT :LENGTH [MAKE "NEWWORD LPUT "- :NEWWORD]
MAKE "DEP -22 - (11 * (:LENGTH - 3))
MAKE "DEP1 :DEP - 22
END

TO HANGER 
SETPC 1 PD
LT 90
FD 40
BK 20
RT 90
FD 115
RT 90
FD 50
RT 90
FD 10
SETPC 3
TITLE :DEP -40 :NEWWORD
END

TO PLAY 
IF OR NOT MEMBERP "\- :NEWWORD EMPTYP :PART [STOP]
TYPE [GUESS A LETTER :]
MAKE "LET RC
IF MEMBERP :LET :GIVEN [PR [\ YOU ALREADY GUESSED] PR [\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ THAT LETTER]] [NEW CHECKANS]
PLAY
END

TO WORDS 
MAKE "WORDS [TRAIN PLACE WORLD COMPUTER WORD VILLAGE RHYME HOUSE WATCH MORNING WOMAN TEACH POINT TRUCK]
OUTPUT ITEM 1 + RANDOM COUNT :WORDS :WORDS
END

TO NEW 
MAKE "GIVEN LPUT :LET :GIVEN
CT
SETCURSOR [20 2] PR SE [GIVEN :] :GIVEN
SETCURSOR [22 0]
END

TO CHECKANS 
MAKE "ANS CHECK :LET :WORD 1
TEST EMPTYP :ANS
IFTRUE [PR [YOU MUST HANG] HANG]
IFFALSE [REPEAT COUNT :ANS [MAKE "NEWWORD REPLACE FIRST :ANS :LET :NEWWORD TITLE (:DEP1 + 22 * FIRST :ANS) -30 :LET MAKE "ANS BF :ANS]]
END

TO CHECK :LET :WORD :N
IF EMPTYP :WORD [OP []]
IF :LET = FIRST :WORD [OP SE :N CHECK :LET BF :WORD :N + 1]
OP CHECK :LET BF :WORD :N + 1
END

TO HANG 
RUN SE FIRST :PART
MAKE "PART BF :PART
END

TO REPLACE :PLACE :LET :WORD
IF :PLACE = 1 [OP SE :LET BF :WORD]
OP SE FIRST :WORD REPLACE :PLACE - 1 :LET BF :WORD
END

TO NECK 
PU SETH 180 SETPOS [30 86] PD
FD 11
END

TO BODY 
PU SETPOS [30 75] SETH 180 PD
FD 30
END

TO CIRCLE :SIZE
REPEAT 18 [FD :SIZE RT 20]
END

TO HEAD 
PU SETPOS [30 105] PD
SETH 90
BK 2
CIRCLE 3
PU SETPOS [30 92] PD
FILL
END

TO LTLEG 
PU SETPOS [30 45] PD
SETH 215 FD 30
END

TO RTLEG 
PU
SETPOS [30 45]
SETH 145
PD
FD 30
END

TO RTARM 
PU SETPOS [30 75]
PD SETH 135
FD 20
END

TO LTARM 
PU SETPOS [30 75]
PD SETH 225
FD 20
END

TO START 
EXPL
WAITKEY
MAKE "DUMMY RC
IF :DUMMY = CHAR 27 [CS CT HT DEMO STOP]
HANGMAN
END

