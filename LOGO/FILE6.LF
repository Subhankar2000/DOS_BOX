TO POLYSPI :SIDE :ANGLE :INC :SIZE
FD :SIDE
RT :ANGLE
IF :SIDE > :SIZE [STOP]
POLYSPI :SIDE + :INC :ANGLE :INC :SIZE
END

TO C :SIZE :LEVEL
IF :LEVEL = 0 [FD :SIZE STOP]
C :SIZE :LEVEL - 1
RT 90
C :SIZE :LEVEL - 1
LT 90
END

TO ONESIDE :LEVEL
IF :LEVEL = 0 [STOP]
ONESIDE :LEVEL - 1
RT 45
FD :DIAG
RT 45
ONESIDE :LEVEL - 1
LT 90
FD :SIDE
LT 90
ONESIDE :LEVEL - 1
RT 45
FD :DIAG
RT 45
ONESIDE :LEVEL - 1
END

TO SIERPINSKI :SIDE :LEVEL
MAKE "DIAG :SIDE / SQRT 2
REPEAT 4 [ONESIDE :LEVEL RT 45 FD :DIAG RT 45]
END

TO HILBERT :SIZE :LEVEL :PARITY
IF :LEVEL = 0 [STOP]
LT :PARITY * 90
HILBERT :SIZE :LEVEL - 1 (0 - :PARITY)
FD :SIZE
RT :PARITY * 90
HILBERT :SIZE :LEVEL - 1 :PARITY
FD :SIZE
HILBERT :SIZE :LEVEL - 1 :PARITY
RT :PARITY * 90
FD :SIZE
HILBERT :SIZE :LEVEL - 1 (0 - :PARITY)
LT :PARITY * 90
END

TO LDRAGON :SIZE :LEVEL
IF :LEVEL = 0 [FD :SIZE STOP]
LDRAGON :SIZE :LEVEL - 1
LT 90
RDRAGON :SIZE :LEVEL - 1
END

TO RDRAGON :SIZE :LEVEL
IF :LEVEL = 0 [FD :SIZE STOP]
LDRAGON :SIZE :LEVEL - 1
RT 90
RDRAGON :SIZE :LEVEL - 1
END

TO REPEAT?
CT
PR [Press any key to continue, Esc to exit]
MAKE "KEY RC
IF :KEY = CHAR 27 [CS CT HT DEMO]
END

TO START 
CT CS HT SETTEXT 2
SETCURSOR [5 8]
PR [RECURSIVE PATTERNS]
SETCURSOR [8 3]
PR [Type the number, and LOGO will draw]
SETCURSOR [9 3]
PR [the pattern of your choice.]
SETCURSOR [12 7]
PR [1 HILBERT CURVE]
SETCURSOR [13 7]
PR [2 SIERPINSKI CURVE]
SETCURSOR [14 7]
PR [3 POLYSPI 92]
SETCURSOR [15 7]
PR [4 POLYSPI 121]
SETCURSOR [16 7]
PR [5 DRAGON CURVE]
SETCURSOR [17 7]
PR [6 C CURVE]
SETCURSOR [20 5]
PR [Esc TO RETURN TO THE MAIN MENU]
WAITKEY
MAKE "KEY RC
IF :KEY = CHAR 27 [DEMO]
IF :KEY = 1 [HILB.CURV]
IF :KEY = 2 [SIERP.CURV]
IF :KEY = 3 [POLY1]
IF :KEY = 4 [POLY2]
IF :KEY = 5 [DRAG.CURV]
IF :KEY = 6 [C.CURV]
START
END

TO HILB.CURV
CS
SETPC 3
TITLE -78 100 [HILBERT CURVE]
HT PU
SETPOS [70 -85]
PD
HILBERT 5 5 1
REPEAT?
END


TO DRAG.CURV
CS
TITLE -72 100 [DRAGON CURVE]
HT PU
SETPOS [40 -30]
PD
LDRAGON 4 10
REPEAT?
END

TO C.CURV
CS
TITLE -45 100 [C CURVE]
HT PU
SETH -90
SETPOS [20 -40]
PD
C 2 10
REPEAT?
END

TO POLY2
CS
TITLE -70 100 [POLYSPI 121]
HT PU
HOME
PD
POLYSPI 1 121 2 160
REPEAT?
END


TO POLY1
CS
TITLE -55 100 [POLYSPI 92]
HT PU
HOME
PD
POLYSPI 1 92 1 140
REPEAT?
END


TO SIERP.CURV
CS
TITLE -95 100 [SIERPINSKI CURVE]
HT
PU
SETPOS [-100 -100]
PD
SIERPINSKI 6 4
REPEAT?
END

