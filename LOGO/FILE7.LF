TO RULES 
IF KEYP [CLEARBUFF]
CS CT HT
SETCURSOR [5 7]
PR [MUSICAL MEMORY GAME]
SETCURSOR [9 2]
PR [This is a game in which the computer]
PR [plays a note, or a sequence of notes,]
PR [and you repeat them. The sequence]
PR [becomes longer each time you respond]
PR [correctly. The screen is laid out like]
PR [the numeric keypad on the right]
PR [of the keyboard.]
PR " 
PR [Press the Num Lock key to use the]
PR [numeric keypad for playing notes.]
PR [Press any key to begin, or Esc]
PR [to return to the main menu.]
WAITKEYP
END

TO INIT 
MAKE "1 493.88
MAKE "2 523.23
MAKE "3 587.33
MAKE "4 659.26
MAKE "5 698.46
MAKE "6 783.99
MAKE "7 880
MAKE "8 987.77
MAKE "9 1046.5
MAKE "0 1174.7
MAKE "11 [-40 -40]
MAKE "12 [0 -40]
MAKE "13 [40 -40]
MAKE "14 [-40 0]
MAKE "15 [0 0]
MAKE "16 [40 0]
MAKE "17 [-40 40]
MAKE "18 [0 40]
MAKE "19 [40 40]
MAKE "PLAYLIST []
GEN.PLAYLIST
RECYCLE
END

TO DRAWKEYS 
CT CS MS
SETPC 2
SETTEXT 2
TITLE -100 95 [MUSICAL MEMORY GAME]
PU ST
SETSHAPE 219
MAKE "INC 11
REPEAT 9 [SETPOS THING :INC BLOCK MAKE "INC :INC + 1]
SETPC 3
HT
IF KEYP [CLEARBUFF]
PR [Press 3 notes to hear the whole tune]
REPEAT 3 [KEYPLAY]
PR " PR "
MAKE "INC 1
WAIT 20
REPEAT 30 [LISTPLAY]
PR [OK, let's begin...]
WAIT 50
END

TO SORRY 
TONE 200 10
MAKE "ERRORS :ERRORS + 1
PR SE :ERRORS [WRONG]
IF :ERRORS > 2 [THROW "MORE?]
RECYCLE
END

TO CHECK 
IF NOT ITEM :INC :PLAYLIST = :KEY [MAKE "OK "FALSE SORRY THROW "OOPS]
MAKE "INC :INC + 1
END

TO LISTPLAY 
MAKE "KEY ITEM :INC :PLAYLIST
PLAY
MAKE "INC :INC + 1
END

TO KEYPLAY 
MAKE "KEY RC
IF :KEY = "\  [KEYPLAY]
IF NUMBERP :KEY = "TRUE [PLAY]
END

TO PLAY 
TONE THING :KEY 7
MAKE "POS :KEY + 10
SETSHAPE :KEY + 48
SETPOS THING :POS
ST
WAIT 4
HT
END

TO BLOCK 
SETH 0
SETPC 1
FD 9
RT 90
REPEAT 4 [STAMP FD 4 STAMP FD 5 STAMP RT 90 FD 4 STAMP FD 5 STAMP]
END

TO PLAYTHEGAME 
MAKE "OK "TRUE
MAKE "INC 1
PR [MY TURN]
REPEAT :TIMES [LISTPLAY]
IF KEYP [CLEARBUFF]
MAKE "INC 1
SETCURSOR SE FIRST CURSOR 10 PR [YOUR TURN]
CATCH "OOPS [REPEAT :TIMES [KEYPLAY CHECK]]
IF :OK = "TRUE [SETCURSOR SE FIRST CURSOR 18 PR [GOOD!] RECYCLE MAKE "TIMES :TIMES + 1]
CATCH "MORE? [PLAYTHEGAME]
ONEMORETIME
END

TO ONEMORETIME 
IF KEYP [CLEARBUFF]
WAIT 20
PR [Press any key to continue, Esc to exit]
MAKE "DUMMY RC
IF :DUMMY = CHAR 27 [CS CT HT DEMO]
PR [OK, let's try another]
WAIT 20
GEN.PLAYLIST PLAYTHEGAME
END

TO GEN.PLAYLIST 
MAKE "ERRORS 0
MAKE "TIMES 1
REPEAT 30 [MAKE "PLAYLIST FPUT 1 + RANDOM 9 :PLAYLIST]
END

TO WAITKEYP 
IF KEYP [STOP]
WAITKEYP
END

TO CLEARBUFF 
MAKE "DUMMY RC
IF KEYP [CLEARBUFF]
END

TO START 
REPARSE
RULES
MAKE "DUMMY RC
IF :DUMMY = CHAR 27 [CS CT HT DEMO]
INIT
DRAWKEYS
PLAYTHEGAME
END

